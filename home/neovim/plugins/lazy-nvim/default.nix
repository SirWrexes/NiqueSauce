{
  config,
  pkgs,
  lib,
  ...
}:

let
  lazylib = import ./lib { inherit lib; };

  inherit (lazylib) types toLua;
  inherit (lazylib.options) mkOption mkPackageOption mkDescribedEnableOption;
  inherit (lazylib.modules) generatedModulesDir;

  cfg = config.programs.neovim.lazy-nvim;

  fullModulesDirPath = "~/.config/nvim/${generatedModulesDir}";
in
{
  imports = [
    ./lazy-spec.nix
    ./lspconfig.nix
  ];

  options.programs.neovim.lazy-nvim = {
    enable = mkDescribedEnableOption "LazyVim plugin manager for NeoVim" ''
      Important note:
        While configuring this module, you might come accross options that have descriptions
        mentioning Lua function signatures as their accepted type. It means that the option,
        if you chose to use Lua code for it, must receive a value of type `lib.types.luaInline`
        or a path to a Lua file that will be read by this module.

        Aside from the type checking of the Nix value, there is no way to verify what you
        pass to those options is valid Lua code. It is up to you to make sure that you properly
        wrote your Lua snippet.

      Lastly, I highly recommend checking out the official docs for the plugin: https://lazy.folke.io/

      Enjoy!
    '';

    lazyByDefault = mkDescribedEnableOption "lazy-loading for ALL plugins by default" ''
      Make all plugins lazy by default. Note that this could end up in some plugins never actually
      activating, or worse even, wrong order of loading which could result in broken dependcy chains.

      Quote from Folke's docs:
      > Set this to `true` to have all your plugins lazy-loaded by default.
      > Only do this if you know what you are doing, as it can lead to unexpected behavior.

      See https://lazy.folke.io/configuration
    '';

    defaultEnablePredicate =
      with types;
      mkOption {
        type = nullOr luaPredicate;
        default = null;
        readOnly = true;
        description = ''
          Default `cond` that is assigned to all plugins. You can use it to globally disable plugins in bulk.
          When false, or if the function returns false, then the plugin will not be included in the spec.

          Lazy.nvim ignores extraneous keys on your specs, so you could, for instance, set a common flag
          to check inside them in order to then evaluate some condition to decide if a plugin must
          be activated or not.

          Expected luaSnippet content type:
          ```
          fun(self: LazyPlugin): boolean?
          ```
        '';
        example = literalExpression ''
          lib.types.mkLuaInline ''''
            function (self)
              if jit.os:find("macos") and self.disableOnMac == true then
                return false
              end
            end
          ''''
        '';
      };

    dashboardKeymap =
      with types;
      mkOption {
        type = nullOr str;
        default = "<S-F1>";
        readOnly = true;
        description = ''
          Set the key for opening the Lazy dashboard.
          Will be mapped to the command `:Lazy`.
        '';
      };

    # TODO: Find how to properly run the things on home files
    # transformGeneratedLua =
    #   with types;
    #   mkOption {
    #     type = nullOr (enum [
    #       "pretty"
    #       "mini"
    #     ]);
    #     default = null;
    #     description = ''
    #       `null` or undefined: Keep generated sources as is.
    #       "pretty": Will run `stylua` on the generated sources.
    #       All sources generated by this module can be found in `${fullModulesDirPath}`.
    #     '';
    #   };

    toLua =
      with types;
      mkOption {
        type = functionTo str;
        default = toLua;
        readOnly = true;
        description = ''
          Generator used to convert expressions to Lua.
          Use this insead of `lib.generators.toLua` for consistency between the code generated by this module and yours.
        '';
      };

    package = mkPackageOption pkgs [ "vimPlugins" "lazy-nvim" ] { };
  };

  config =
    lib.mkIf
      (
        cfg.enable
        && lib.asserts.assertMsg config.programs.neovim.enable ''
          Hey there ! 󱠡
          I see you're trying to use Lazy.nvim, but it seems NeoVim is not enabled in your home manager config.
          Please enable it and try again.

          󱩖 Put this somewhere in your *home manager* config:
            programs.neovim.enable = true;
        ''
      )
      {
        # TODO: Find a way to format sources, probably using `home.activation`

        programs.neovim.extraLuaPackages = luapkgs: with luapkgs; [ rocks-nvim ];
        programs.neovim.extraPackages = [ cfg.package ];

        # Keep the empty lines above and below the lua code to prevent the statements
        # sticking to other lines during concatenation.
        programs.neovim.extraLuaConfig =
          # lua
          ''

            if not vim.g.lazy_did_setup then
              vim.opt.rtp:prepend("${cfg.package}")

              vim.keymap.set('n', ${toLua cfg.dashboardKeymap},'<cmd>Lazy<cr>', {
                silent = true,
                noremap = true,
              })

              require("lazy").setup(${
                toLua {
                  # Spec definition and plugin package install is defined in laz-spec.nix
                  inherit (cfg) spec;

                  # Disable automatic plugin updates.
                  # Plugins will be installed in the Nix store, as opposed to the usual Git flavoured way Lazy.nvim normally uses.
                  checker.enable = false;

                  # Prevent Lazy.nvim from installing missing plugins.
                  # Since we're bypassing the Git installation of plugins, auto-installation shouldn't do anything.
                  install.missing = true;

                  lazy = cfg.lazyByDefault;
                  cond = cfg.defaultEnablePredicate;

                  ui.border = "rounded";
                }
              })
            end

          '';
      };
}
